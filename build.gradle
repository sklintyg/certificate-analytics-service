plugins {
    alias(libs.plugins.org.cyclonedx.bom)
    alias(libs.plugins.org.springframework.boot)
    alias(libs.plugins.org.sonarqube)
    alias(libs.plugins.org.owasp.dependencycheck)
    alias(libs.plugins.com.github.ben.manes.versions)
    id 'test-report-aggregation'
    id 'application'
}

ext {
    intygBomVersion = System.properties['intygBomVersion'] ?: '0.0.1-SNAPSHOT'
    ineraMavenRepository = 'https://nexus.drift.inera.se/repository/it-public/'
    nvdMirror = 'https://vulnz-nvd-mirror-devtest.intyg.nordicmedtest.se'
}

springBoot {
    mainClass = 'se.inera.intyg.certificateanalyticsservice.CertificateAnalyticsServiceApplication'
}
group = 'se.inera.intyg.certificateanalyticsservice'
version = System.properties['buildVersion'] ?: '0.0.1-SNAPSHOT'

apply from: 'gradle/configureIntygBom.gradle'

allprojects {
    repositories {
        mavenLocal()
        maven { url = ineraMavenRepository }
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'java-test-fixtures'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(javaVersion)
        }
    }

    configurations {
        mockitoAgent
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        implementation platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        annotationProcessor platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        testImplementation platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        testAnnotationProcessor platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        testFixturesImplementation platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        mockitoAgent platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        mockitoAgent("org.mockito:mockito-core") { transitive = false }
    }

    jacocoTestReport {
        dependsOn test

        reports {
            xml.required = true
            html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        }

        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        "**/testability/**",
                        "**/integrationtest/**",
                ])
            }))
        }
    }

    tasks.matching { it.name == "cyclonedxDirectBom" }.configureEach { t ->
        t.includeConfigs = ['^runtimeClasspath$']
    }

    test {
        jvmArgs += [
                "-javaagent:${configurations.mockitoAgent.asPath}",
                "-Xshare:off"
        ]
    }
}

reporting {
    reports {
        integrationTestAggregateTestReport(AggregateTestReport) {
            testSuiteName = 'integrationTest'
        }
    }

    subprojects.collect { subproject -> subproject.name }.each { subprojectName ->
        dependencies { testReportAggregation project(":${subprojectName}") }
    }
}

sonar {
    properties {
        property "sonar.projectKey", "intyg-certificate-analytics-service"
        property "sonar.projectName", "intyg-certificate-analytics-service"
        property "sonar.exclusions", "**/integration/api/**/*,**/testability/**,**/dto/**/*,**/integrationtest/**"
    }
}